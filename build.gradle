plugins {
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("io.micronaut.application") version "3.7.10"
    id("jacoco")
    id("org.sonarqube") version "4.2.1.3168"
    id("pl.allegro.tech.build.axion-release") version "1.15.3"
}

version = scmVersion.version
group = "com.michelin.kafkactl"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("info.picocli:picocli-codegen")
    annotationProcessor("org.projectlombok:lombok")

    implementation("info.picocli:picocli")
    implementation("io.micronaut.picocli:micronaut-picocli")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut:micronaut-validation")
    implementation("jakarta.annotation:jakarta.annotation-api")
    implementation("io.micronaut:micronaut-http-client")
    implementation("org.ocpsoft.prettytime:prettytime:5.0.6.Final")
    implementation("io.github.java-diff-utils:java-diff-utils:4.12")

    runtimeOnly("ch.qos.logback:logback-classic")

    compileOnly("org.projectlombok:lombok")
    compileOnly("org.graalvm.nativeimage:svm")

    testImplementation("io.micronaut:micronaut-http-client")
    testImplementation("org.mockito:mockito-junit-jupiter:5.3.1")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.9.3")
    testImplementation("org.mockito:mockito-core")

    testAnnotationProcessor("org.projectlombok:lombok")
    testCompileOnly("org.projectlombok:lombok")
}

application {
    mainClass.set("com.michelin.kafkactl.KafkactlCommand")
}

java {
    sourceCompatibility = JavaVersion.toVersion("11")
    targetCompatibility = JavaVersion.toVersion("11")
}

micronaut {
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("com.michelin.kafkactl.*")
    }
}

shadowJar {
    archiveClassifier.set('')
    dependsOn(distTar, distZip)
}

graalvmNative {
    binaries {
        main {
            imageName.set("kafkactl-" + project.version)
            buildArgs.add("-H:Class=com.michelin.kafkactl.KafkactlCommand")
            buildArgs.add("-H:IncludeResourceBundles=org.ocpsoft.prettytime.i18n.Resources")
            buildArgs.add("-H:+ReportUnsupportedElementsAtRuntime")
            buildArgs.add("-H:+ReportExceptionStackTraces")
            buildArgs.add("-H:+TraceNativeToolUsage")
            buildArgs.add("-H:Log=*ShimDLL:3,link:3,copy:3")
        }
    }
}

dockerfile {
    baseImage("eclipse-temurin:11-jre-alpine")
}

dockerfileNative {
    baseImage("ubuntu:22.04")
    editDockerfile {
        after('COPY --from=graalvm /home/app/application /app/application') {
            insert('RUN ln -s /app/application /usr/bin/kafkactl && chmod 777 /usr/bin/kafkactl')
        }
    }
}

if (project.hasProperty("releaseLatest")) {
    dockerBuild {
        images = ["michelin/kafkactl:" + version, "michelin/kafkactl:latest"]
    }

    dockerBuildNative {
        images = ["michelin/kafkactl:" + version + "-native", "michelin/kafkactl:latest-native"]
    }
} else {
    dockerBuild {
        images = ["michelin/kafkactl:" + version]
    }

    dockerBuildNative {
        images = ["michelin/kafkactl:" + version + "-native"]
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "michelin_kafkactl"
        property "sonar.organization", "michelin"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        html.enabled true
        xml.enabled true
    }
}

import org.apache.tools.ant.filters.ReplaceTokens
processResources {
    filesMatching("application.yml") {
        filter(ReplaceTokens, tokens: [version: project.version])
    }
}
